/*
	Add your SAP Business ByDesign scripting language implementation for:
		Business Object: BusinessObjectEvent
		Node: Root
		Event: BeforeSave 
		
	Note: 
	  - To access the elements of the business object node, 
	    use path expressions, for example, this.<element name>. 
	  - To use code completion, press CTRL+J. 
	  - This script file is mass-enabled, i.e. the keyword "this" 
	    refers to a collection of node instances.
	  - The solution uses this script when:
		- the instance of the business object is being saved.
		- the instance of the business object is created from other sources like web services, preview screen, and so on.
*/

import ABSL;

var event;
foreach (event in this)
{	
	//Loading the association ToObjectEventConfig if not yet, which will be used to populate the publicaiton to event.
	if (!event.ToObjectEventConfig.IsSet())
	{
		var objEvtConfig = ObjectEventConfig.Retrieve(event.ObjectType);
		event.RealtimePublication = objEvtConfig.RealtimePublication;
		var objPubChannelList : collectionof ObjectEventConfig.Publication;
		if (objEvtConfig.IsSet())
		{
			objPubChannelList = objEvtConfig.Publication;
		}
		else
		{
			Trace.Error("Event Configuration not found for object", event.ObjectType);
			continue;
		}
		
		//handling the case of event is updated by manual.
		if (event.Publication.Count() > 0)
		{
			Trace.Info("Publication exists on the event, just keep them", event.EventID.TrimLeft("0"));

			//Immediate process
			if (event.RealtimePublication)
				event.Publish();

			//do nothing and keep the original publications
			continue;
			//or Simply delete once the event is updated.
			//event.Publication.Delete();
		}

		var objPubChannel;
		foreach (objPubChannel in objPubChannelList)
		{
			var publicationNode : elementsof BusinessObjectEvent.Publication;
			publicationNode.PublicationChannelID = objPubChannel.ChannelID;
			publicationNode.PublicationStatus = EventPublicationStatusCode.CO_00_NEW;
			var publicationInst = event.Publication.Create(publicationNode);
		}

		//Immediate process
		if (event.RealtimePublication)
			event.Publish();
	}
}